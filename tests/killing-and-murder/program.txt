// let p = 22;
// let q = 44;
// let mut x: &mut i32 = &mut p; // `x` points at `p`
// let y = &mut *x; // Loan L0, `y` points at `p` too
// // ...
// x = &mut q; // `x` points at `q`; clears L0
// use(x); // OK, because `x` no longer borrows `p`
// *y = 11; // also OK

// Decls
// let p: u32
// let q: u32
// let x: &'x mut u32
// let y: &'y mut u32
// 
// Loan origins:
// 'L_p: `x`'s borrow of `p` 
// 'L_*x: `y`'s borrow of `*x`
// 'L_q: `x`'s borrow of `q`

a: "p = 22" {
    invalidate_origin('L_p)
    goto b
}

b: "q = 44" {
    invalidate_origin('L_q)
    goto c
}

c: "x = &'L_p mut p" {
    // Clear all origins in `x` (overwritten plan):
    clear_origin('x)

    // Clear 'L_p because it's being recreated
    clear_origin('L_p)

    // `&'L_p mut u32 <: &'x mut u32`
    introduce_subset('L_p, 'x) 
    goto d
}

d: "y = &'L_*x mut *x" {
    // Access `*x` [== `p`] where `x: &'x mut u32`
    access_origin('x)
    invalidate_origin('L_*x)

    // Clear all origins in `y` (overwritten plan):
    clear_origin('y)

    // Clear 'L_*x because it's being recreated
    clear_origin('L_*x)

    // Unroll `*x`
    introduce_subset('x, 'L_*x)

    // `&'L_*x mut u32 <: &'y mut u32`
    introduce_subset('L_*x, 'y) 
    goto e
}

e: "x = &'L_q mut q" {
    // Clear all origins in `x` (overwritten plan):
    clear_origin('x)

    // Clear 'L_q because it's being recreated
    clear_origin('L_q)

    // Clear 'L_*x because `x` changed, so future invalidations of `x` no longer affect 'L_*x
    clear_origin('L_*x)

    // `&'L_q mut u32 <: &'x mut u32`
    introduce_subset('L_q, 'x) 
    goto f
}

f: "use(x)" {
    // Access `x` [== `&mut q`] where `x: &'x mut u32`
    access_origin('x)

    // Invalidate borrows that contain the used path `x`
    invalidate_origin('L_*x)

    // `x` is moved into `use`
    kill_origin('x)
    
    goto g
}

g: "*y = 11" {
    // Access `*y` [== `p`] where `y: &'y mut u32`
    access_origin('y)

    goto
}